;----------------------------------------------------------------
; struct handle *realloc(struct handle *handle, uint16 size)
; return pointer to allocated handle or zero if error
;----------------------------------------------------------------
:realloc
	set push, 2
	set push, 0
	jsr preamble

	set a, [z+11]
	ifl [a + HANDLE_SIZE], [z + 10]
		set pc, realloc__enlarge

	; shrink is easy
	set [a + HANDLE_SIZE], [z+10]	
	set pc, postamble
		
:realloc__enlarge
	; calculate required extra space
	set c, [z + 10]				; c = new_size - old_size
	sub c, [a + HANDLE_SIZE]
	
	; calculate available space
	set b, [heap_handle_start]
	sub b, [heap_free_start]

	ifl b, c						; if available space < required space
		set pc, realloc__garbage_collect

	set [a + HANDLE_SIZE], [z + 10]
	jsr compact
	
	set pc, postamble

:realloc__garbage_collect
	jsr mark
	jsr compact
	
	; calculate available space
	set b, [heap_handle_start]
	sub b, [heap_free_start]

	ifl b, c						; if available space < required space
		jsr recover
	
	set [a + HANDLE_SIZE], [z + 10]
	jsr compact
	
	set pc, postamble

;----------------------------------------------------------------
; struct handle *alloc(uint16 size, uint16 type)
; return pointer to allocated handle or zero if error
;----------------------------------------------------------------
:alloc
	set push, 2
	set push, 0
	jsr preamble

	; calculate required heap space
	set a, [z+11]
	add a, SIZEOF_HANDLE

	; calculate space in heap gap
	set b, [heap_handle_start]
	sub b, [heap_free_start]

	ifl b, a						; if available space < required space
		set pc, alloc__garbage_collect

	;---------------------------------------
	; handle cleanup
	;---------------------------------------
	add [heap_counter], 1
	ife [heap_counter], HEAP_GC_TRIGGER
		jsr mark

	set pc, alloc__reserve_handle

:alloc__garbage_collect
	jsr mark
	jsr compact
	
	set b, [heap_handle_start]
	sub b, [heap_free_start]

	ifl b, a							; if available space < required space
		jsr recover
	
:alloc__reserve_handle
	set c, [heap_handle_free]

	; remove handle from free list
	ifn c, 0
		set [heap_handle_free], [c + HANDLE_NEXT]
    ; ...and skip handle allocation
	ifn c, 0							; recycled handle available?
		set pc, alloc__reserve_data		; yes

	set c, [heap_handle_start]			; reserve new handle
	sub c, SIZEOF_HANDLE

	set [heap_handle_start], c

:alloc__reserve_data
	; c points now to new handle structure

	; init allocated space
	set i, [heap_free_start]			; i points to allocated memory region

	set a, i
	add a, [z+11]                       ; a points to first word after allocated memory

:alloc__init_memory
    ; initialize allocated space with all zeros
	ife i, a
		set pc, alloc__finish

	set [i], 0				            ; default init with zero
	sti pc, alloc__init_memory

:alloc__finish
    ; initialize handle structure
	set [c + HANDLE_PTR], [heap_free_start]     ; pointer to allocated memory region
	set [c + HANDLE_SIZE], [z+11]				; size of allocated memory region
	set [c + HANDLE_TYPE], [z+10]				; type of data that is pointed by this handle
	set [c + HANDLE_NEXT], 0					; next handle in reserved handles list

	set [heap_free_start], a					; update unallocated heap space pointer

	; append new handle to reserved handles list
	set b, [heap_handle_tail]					; store pointer to last item in reserved handles list
	set [heap_handle_tail], c					; set reserved handles tail pointer to point in new handle
	set [b + HANDLE_NEXT], c					; set previous tail's next to point to new handle
	
	set x, c									; return pointer to new handle
	set pc, postamble	

;----------------------------------------------------------------
; void compact()
;----------------------------------------------------------------
:compact
	set push, 0
	set push, 0
	jsr preamble

	set i, HEAP_START

	set a, heap_handle_head			; struct handle *
	sub a, HANDLE_NEXT
	
:compact_loop
	set a, [a + HANDLE_NEXT]
	ife a, 0
		set pc, compact_finish

	set j, [a + HANDLE_PTR]		; copy from

	set b, i			; calculate end pointer
	add b, [a + HANDLE_SIZE]

	set [a + HANDLE_PTR], i

	ife i, j			; if src == dst
		set i, b		; skip copy

:compact_copy_loop
	ife i, b
		set pc, compact_loop
	sti [i], [j]
	set pc, compact_copy_loop

:compact_finish
	set [heap_free_start], i
	set pc, postamble

;----------------------------------------------------------------
; void free_handles()
;----------------------------------------------------------------
:mark
	set push, 0
	set push, 0
	jsr preamble

	; reset handle reservation counter
	set [heap_counter], 0

	set y, z				; y: temp fp

:mark__process_frame
	; i = y - number_of_references
	set i, y 
	sub i, [y + 7]

:mark__loop_references
	ife i, y							; are all references processed?
		set pc, mark__advance

	set push, [i]
	jsr mark_handle
	
	sti pc, mark__loop_references

:mark__advance
	; set y to next frame pointer in call stack
	set y, [y]

	; continue loop if more frames
	ifn y, 0
		set pc, mark__process_frame

	;---------------------------------------
	; done marking,
	; reconstruct reserved and free lists
	;---------------------------------------
	; use j to iterate over reserved handles list
	set i, heap_handle_head			; struct handle *
	set j, [i]
	sub i, HANDLE_NEXT
	
:mark__free_loop
	; this loop 
	; 1) removes marked-flag from marked handles
	; 2) inserts each handle to reserved or free list

	ife j, 0						; if end of list
		set pc, mark__free_loop__break

	ifb [j + HANDLE_TYPE], TYPE_MARKED
		set pc, mark__free_loop__marked

:mark__free_loop__unmarked
	; store pointer to next handle
	set x, [j + HANDLE_NEXT]

	; add item to free list
	set [j + HANDLE_NEXT], [heap_handle_free]	; add to free list
	set [heap_handle_free], j

	; set j to next handle in reserved list
	set j, x

	set pc, mark__free_loop

:mark__free_loop__marked
	; clear marked flag
	xor [j + HANDLE_TYPE], TYPE_MARKED

	; set next pointer in previous reserved handle
	set [i + HANDLE_NEXT], j

	; advance previous handle pointer
	set i, j

	; advance handle pointer
	set j, [j + HANDLE_NEXT]

	set pc, mark__free_loop

:mark__free_loop__break
	set [i + HANDLE_NEXT], 0
	set [heap_handle_tail], i
	set pc, postamble

;----------------------------------------------------------------
; void free_handles__mark(struct handle *item)
; recursively sets the mark bit in item and contained items
;----------------------------------------------------------------
:mark_handle
	; this function uses standard call convention, but optimized implementation
	set x, [sp + 1]						; struct handle *item

	; remove item from stack (preserve rts)
	set [sp], pop
	
	;---------------------------------------
	; return if handle is outside HEAP handles
	;---------------------------------------
	ifl x, [heap_handle_start]
		set pc, pop
	
	ifg x, HEAP_END - SIZEOF_HANDLE
		set pc, pop
	;---------------------------------------

	ifb [x + HANDLE_TYPE], TYPE_MARKED	; return if handle is already marked
		set pc, pop

	bor [x + HANDLE_TYPE], TYPE_MARKED	; mark handle

	; return if handle is not container
;	ifc [x + HANDLE_TYPE], TYPE_CONTROL + TYPE_REFERENCE + TYPE_SUBSCRIPTION + TYPE_TUPLE + TYPE_LIST + TYPE_DICT
	ifc [x + HANDLE_TYPE], TYPE_GROUP_CONTAINER
		set pc, pop

	set push, x
	set push, mark_handle		; map() function for iterate
	jsr array_map

	set pc, pop
